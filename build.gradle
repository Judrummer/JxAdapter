// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.4-2"
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
    }
}

allprojects {
    ext {
        minSdk = 16
        targetSdk = 25

        groupName = "com.github.judrummer.jxadapter"

        supportVersion = '25.1.1'
        kotlinVersion = '1.1.4-2'
        supportTestVersion = '0.4.1'
        kxandroidVersion = '0.2.0'

    }

    repositories {
        maven { url "https://jitpack.io" }
        jcenter()
    }
}


configure(subprojects - project(':example')) {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'com.github.dcendents.android-maven'

    group = parent.ext.groupName

    android {
        compileSdkVersion 25
        buildToolsVersion "25.0.2"

        defaultConfig {
            minSdkVersion minSdk
            targetSdkVersion targetSdk
            versionCode 1
            versionName "1.0"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }
    dependencies {
        compile("org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
                "com.android.support:support-v4:$supportVersion",
                "com.android.support:appcompat-v7:$supportVersion",
                "com.android.support:recyclerview-v7:$supportVersion")
        testCompile("junit:junit:4.12",
                "org.robolectric:robolectric:3.1.4",
                "com.android.support:appcompat-v7:$supportVersion",
                "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
                "org.hamcrest:hamcrest-all:1.3"
        )
    }


}

task clean(type: Delete) {
    delete rootProject.buildDir
}
